import com.yangchoi.versionbuild.AndroidConfig
import com.yangchoi.versionbuild.LibsAndroid
import com.yangchoi.versionbuild.LibsAndroidX
import com.yangchoi.versionbuild.LibsGitHub
import com.yangchoi.versionbuild.LibsGoogle
import com.yangchoi.versionbuild.LibsKotlin

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion AndroidConfig.compileSdkVersion
    buildToolsVersion AndroidConfig.buildToolsVersion

    defaultConfig {
        applicationId AndroidConfig.applicationId
        minSdkVersion AndroidConfig.minSdkVersion
        targetSdkVersion AndroidConfig.targetSdkVersion
        versionCode AndroidConfig.versionCode
        versionName AndroidConfig.versionName

        testInstrumentationRunner LibsAndroidX.AndroidJUnitRunner
        //ARouter
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    if (outputFileName != null
                            && outputFileName.endsWith('.apk')
                            && 'release' == variant.buildType.name) {
                        outputFileName = "${AndroidConfig.applicationId}_${defaultConfig.versionCode}(${defaultConfig.versionName}).apk"
                    }
                }
            }
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    if (outputFileName != null
                            && outputFileName.endsWith('.apk')
                            && 'debug' == variant.buildType.name) {
                        outputFileName = "${AndroidConfig.applicationId}_${defaultConfig.versionCode}(${defaultConfig.versionName}).apk"
                    }
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding = true
    }
    packagingOptions {
        exclude 'META-INF/*'
        exclude 'META-INF/NOTICE' // will not include NOTICE file
        exclude 'META-INF/LICENSE' // will not include LICENSE file
        exclude 'META-INF/INDEX.LIST' // will not include INDEX.LIST file
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/gradle/incremental.annotation.processors'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation LibsKotlin.Kotlin
    implementation LibsAndroidX.AppCompat
    implementation LibsAndroidX.CoreKtx
    implementation LibsGoogle.GoogleMaterial
    implementation LibsAndroidX.ConstraintLayout
    implementation LibsAndroidX.NavigationUI
    implementation LibsAndroidX.NavigationFragment
    testImplementation LibsAndroid.Junit
    androidTestImplementation LibsAndroidX.TestExtJunit
    androidTestImplementation LibsAndroidX.TestEspresso

    if (!singleModule.toBoolean()){
        implementation project(path: ':lib-base')
        implementation project(path: ':lib-network')
        implementation project(path: ':lib-public')
        implementation project(path: ':module-home')
        implementation project(path: ':module-project')
        implementation project(path: ':module-mine')
    }
    //工具类
    implementation LibsGitHub.UtilCodeX
    //刷新组件
    implementation LibsGitHub.RefreshLayoutKernel
    implementation LibsGitHub.RefreshHeadClassic
    implementation LibsGitHub.RefreshFoodClassic
    //Badgeview 右上角红点
    implementation LibsGitHub.Badgeview
    //ARouter
    kapt LibsGitHub.ARouteCompiler

}